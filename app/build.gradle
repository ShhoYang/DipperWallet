def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystorePropertiesFile.withReader('UTF-8') { reader ->
    keystoreProperties.load(reader)
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 13
        versionName "1.0.13"

        multiDexEnabled true
        flavorDimensions "default"
        consumerProguardFiles 'consumer-rules.pro'
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        product {
            applicationId 'com.highstreet.wallet'
            manifestPlaceholders = [
                    APP_ICON: '@mipmap/ic_launcher'
            ]
            buildConfigField 'String', 'BASE_URL', '"https://rpc.dippernetwork.com/"'
            buildConfigField 'Boolean', 'testnet', 'false'
        }
        dev {
            applicationId = 'com.highstreet.wallet.test'
            manifestPlaceholders = [
                    APP_ICON: '@mipmap/ic_launcher_2'
            ]
            buildConfigField 'String', 'BASE_URL', '"https://rpc.testnet.dippernetwork.com/"'
            buildConfigField 'Boolean', 'testnet', 'true'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "Wallet-${defaultConfig.versionName}.apk"
        }
    }

    packagingOptions {
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':lib')
    implementation 'org.bitcoinj:bitcoinj-core:0.15.2'
    implementation 'com.king.zxing:zxing-lite:1.1.9'
    implementation 'com.tencent.bugly:crashreport:latest.release'
    implementation 'cat.ereza:customactivityoncrash:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    kapt compilerDependencies
}